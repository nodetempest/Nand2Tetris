// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Instruction handling

    Not(in=instruction[15], out=AInstructionBit);
    Mux16(a=ALUOut, b=instruction, sel=AInstructionBit, out=ALUOrAIstruction);

    Or(a=instruction[5], b=AInstructionBit, out=loadARegister);
    ARegister(in=ALUOrAIstruction, load=loadARegister, out=ARegisterOut, out[0..14]=addressM);

    // ALU inputs

    Mux16(a=ARegisterOut, b=inM, sel=instruction[12], out=AOrMInput);
    ALU(
        x=DRegisterOut, 
        y=AOrMInput, 
        zx=instruction[11],
        nx=instruction[10],
        zy=instruction[9],
        ny=instruction[8],
        f=instruction[7],
        no=instruction[6],
        out=ALUOut,
        out=outM,
        zr=zr,
        ng=ng
    );
    DRegister(in=ALUOut, load=instruction[4], out=DRegisterOut);

    And(a=instruction[15], b=instruction[3], out=writeM);

    // Control
    Not(in=zr, out=notZr);
    Not(in=ng, out=notNg);

    And(a=instruction[2], b=true, out=J1);
    And(a=instruction[1], b=true, out=J2);
    And(a=instruction[0], b=true, out=J3);

    Not(in=J1, out=notJ1);
    Not(in=J2, out=notJ2);
    Not(in=J3, out=notJ3);

    And3Way(in[0]=notJ1, in[1]=notJ2, in[2]=J3, out=JGTbits);
    And3Way(in[0]=notJ1, in[1]=J2, in[2]=notJ3, out=JEQbits);
    And3Way(in[0]=notJ1, in[1]=J2, in[2]=J3, out=JGEbits);
    And3Way(in[0]=J1, in[1]=notJ2, in[2]=notJ3, out=JLTbits);
    And3Way(in[0]=J1, in[1]=notJ2, in[2]=J3, out=JNEbits);
    And3Way(in[0]=J1, in[1]=J2, in[2]=notJ3, out=JLEbits);
    And3Way(in[0]=J1, in[1]=J2, in[2]=J3, out=JMPbits);

    And(a=notNg, b=notZr, out=JGTcondition);
    And(a=JGTbits, b=JGTcondition, out=JGT);

    And(a=JEQbits, b=zr, out=JEQ);

    And(a=JGEbits, b=notNg, out=JGE);

    And(a=JLTbits, b=ng, out=JLT);

    And(a=JNEbits, b=notZr, out=JNE);

    Or(a=ng, b=zr, out=JLEcondition);
    And(a=JLEbits, b=JLEcondition, out=JLE);

    Or8Way(in[0]=false, in[1]=JGT, in[2]=JEQ, in[3]=JGE, in[4]=JLT, in[5]=JNE, in[6]=JLE, in[7]=JMPbits, out=doJump);
    And(a=instruction[15], b=doJump, out=doJumpIfCinstruction);


    PC(in=ARegisterOut, load=doJumpIfCinstruction, inc=true, reset=reset, out[0..14]=pc);
}